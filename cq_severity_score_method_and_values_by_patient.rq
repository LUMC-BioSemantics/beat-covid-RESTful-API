#+ endpoint: http://lumc-beat-covid.fair-dtls.surf-hosted.nl:7200/repositories/beat-covid
#+ endpoint_in_url: False
#+ description: To answer QC4 - What are the severity score measurements of a patient and which measurement method was used?
#+ defaults:
#+   - type: http://purl.obolibrary.org/obo/NCIT_C47846
#+   - describes: http://purl.obolibrary.org/obo/MONDO_0100096
#+   - dataInputOf: http://purl.obolibrary.org/obo/NCIT_C15206
#+   - hasQuality: http://semanticscience.org/resource/SIO_001410
#+   - patientIdString: "1100207"

# LUMC prefixes
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX dct:     <http://purl.org/dc/terms/>
PREFIX dcat:     <http://www.w3.org/ns/dcat#>
PREFIX obo:     <http://purl.obolibrary.org/obo/>
PREFIX afo:     <http://purl.allotrope.org/ontologies/property#>
PREFIX sio:     <http://semanticscience.org/resource/>
PREFIX prov:    <http://http://http://www.w3.org/ns/prov#>
PREFIX bco:     <http://purl.org/beat-covid/cytokines-semantic-model.owl#>
PREFIX om:      <http://www.ontology-of-units-of-measure.org/resource/om-2/>

SELECT ?severity_score_answer_value
       ?severity_score_measurement_method_id
       ?severity_score_measurement_version_value
       ?scale_min
       ?scale_max WHERE {

      ?beatcovid_dataset a dcat:Dataset ;
                        dcat:type ?_type_iri ;
                        afo:AFX_0002699 ?_describes_iri ;
                        afo:AFX_0000553 ?_dataInputOf_iri ;
                        obo:RO_0000086 ?_hasQuality_iri ;
                        dcat:distribution ?beatcovid_dataset_dist .

      ?beatcovid_dataset_dist a dcat:Distribution ;
                              dcat:accessURL ?accessURL .

      SERVICE ?accessURL {

          ?patient_id a bco:clinical_id  ;
                     obo:IAO_0000219 ?person_patient_role ;
                     sio:SIO_000300 ?_patientIdString .

          ?person_patient_role a sio:SIO_000715 ;
                               sio:SIO_000356 ?severity_score_evaluation_process  .

          ?severity_score_evaluation_process a obo:NCIT_C25214 ;
                                             sio:SIO_000229 ?severity_score_answer ;
                                             dct:conformsTo ?severity_score_measurement_method .

          ?severity_score_answer a obo:CMO_0001109 ;
                                 sio:SIO_000300 ?severity_score_answer_value .

          ?severity_score_measurement_method a obo:NCIT_C81265 ;
                                             sio:SIO_000671 ?severity_score_measurement_method_id ;
                                             dct:hasVersion ?severity_score_measurement_version ;
                                             om:hasScale ?severity_score_result_scale .


          ?severity_score_measurement_version sio:SIO_000300 ?severity_score_measurement_version_value .

          ?severity_score_result_scale a obo:NCIT_C25664 ;
                                       obo:LABO_0000118 ?scale_min ;
      							                   obo:LABO_0000119 ?scale_max .
      }
}
