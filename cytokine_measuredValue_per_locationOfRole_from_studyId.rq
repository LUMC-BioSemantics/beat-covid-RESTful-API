#+ endpoint: http://lumc-beat-covid.fair-dtls.surf-hosted.nl:7200/repositories/fdp
#+ endpoint_in_url: False
#+ description: Query cytokines' measured values per id of ward and hospital from a study id
#+ defaults:
#+   - type: http://purl.obolibrary.org/obo/NCIT_C47846
#+   - describes: http://purl.obolibrary.org/obo/MONDO_0100096
#+   - dataInputOf: http://purl.obolibrary.org/obo/NCIT_C15206
#+   - hasQuality: http://semanticscience.org/resource/SIO_001410
#+   - studyIdString: "BEAT-018"

# LUMC prefixes
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX dct:     <http://purl.org/dc/terms/>
PREFIX dcat:     <http://www.w3.org/ns/dcat#>
PREFIX obo:     <http://purl.obolibrary.org/obo/>
PREFIX afo:     <http://purl.allotrope.org/ontologies/property#>
PREFIX sio:     <http://semanticscience.org/resource/>
PREFIX prov:    <http://http://http://www.w3.org/ns/prov#>
PREFIX bco:     <http://purl.org/beat-covid/cytokines-semantic-model.owl#>


SELECT ?cytokineIdString ?measured_value ?wardId ?hospitalId WHERE {

        ?beatcovid_dataset a dcat:Dataset ;
                          dcat:type ?_type_iri ;
                          afo:AFX_0002699 ?_describes_iri ;
                          afo:AFX_0000553 ?_dataInputOf_iri ;
                          obo:RO_0000086 ?_hasQuality_iri ;
                          dcat:distribution ?beatcovid_dataset_dist .

        ?beatcovid_dataset_dist a dcat:Distribution ;
                                dcat:accessURL ?accessURL .

        SERVICE ?accessURL {

            ?person_study_id a bco:beat_covid_id ;
                          sio:SIO_000300  ?_studyIdString;
                          obo:IAO_0000219 ?person_study_role .

            ?person_study_role a sio:SIO_000883 ;
                                 sio:SIO_000356 ?measurement_process ;
                                 obo:RO_0001025 ?ward .

            ?measurement_process a obo:OBI_0000070 ;
                                sio:SIO_000291 ?trait_cytokine ;
                                sio:SIO_000229 ?quantitative_trait .

            ?trait_cytokine a sio:SIO_010043 ;
                              sio:SIO_000300 ?cytokineIdString .

            ?quantitative_trait a obo:IAO_0000109 ;
                          sio:SIO_000300 ?measured_value .

            ?ward a obo:NCIT_C21541 ;
                  sio:SIO_000300 ?wardId ;
                  obo:BFO_0000050 ?hospital .

            ?hospital a sio:SIO_000688 ;
                      sio:SIO_000300 ?hospitalId .
        }
}
