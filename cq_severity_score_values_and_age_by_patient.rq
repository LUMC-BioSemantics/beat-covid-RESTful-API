#+ endpoint: http://lumc-beat-covid.fair-dtls.surf-hosted.nl:7200/repositories/beat-covid
#+ endpoint_in_url: False
#+ description: To answer QC6 - What are the severity score measurement and age of each patient?
#+ defaults:
#+   - type: http://purl.obolibrary.org/obo/NCIT_C47846
#+   - describes: http://purl.obolibrary.org/obo/MONDO_0100096
#+   - dataInputOf: http://purl.obolibrary.org/obo/NCIT_C15206
#+   - hasQuality: http://semanticscience.org/resource/SIO_001410
#+   - patientIdString: "1100207"

# LUMC prefixes
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX dct:     <http://purl.org/dc/terms/>
PREFIX dcat:     <http://www.w3.org/ns/dcat#>
PREFIX obo:     <http://purl.obolibrary.org/obo/>
PREFIX afo:     <http://purl.allotrope.org/ontologies/property#>
PREFIX sio:     <http://semanticscience.org/resource/>
PREFIX prov:    <http://http://http://www.w3.org/ns/prov#>
PREFIX bco:     <http://purl.org/beat-covid/cytokines-semantic-model.owl#>

SELECT ?person_age_value
       ?severity_score_answer_value
       ?severity_score_evaluation_process_date_value  WHERE {

      ?beatcovid_dataset a dcat:Dataset ;
                        dcat:type ?_type_iri ;
                        afo:AFX_0002699 ?_describes_iri ;
                        afo:AFX_0000553 ?_dataInputOf_iri ;
                        obo:RO_0000086 ?_hasQuality_iri ;
                        dcat:distribution ?beatcovid_dataset_dist .

      ?beatcovid_dataset_dist a dcat:Distribution ;
                              dcat:accessURL ?accessURL .

      SERVICE ?accessURL {

          ?person a sio:SIO_000498 ;
                  sio:SIO_000228 ?person_patient_role ;
                  sio:SIO_000228 ?person_study_role .

          ?person_age a sio:SIO_001013 ;
                      sio:SIO_000001 ?person_study_role ;
                      sio:SIO_000300 ?person_age_value .

          ?patient_id a bco:clinical_id  ;
                      obo:IAO_0000219 ?person_patient_role ;
                      sio:SIO_000300 ?_patientIdString .

          ?person_patient_role a sio:SIO_000715 ;
                               sio:SIO_000356 ?severity_score_evaluation_process  .

          ?severity_score_evaluation_process a obo:NCIT_C25214 ;
                                             sio:SIO_000229 ?severity_score_answer ;
      									                     sio:SIO_000217 ?severity_score_evaluation_process_date .

         ?severity_score_answer a obo:CMO_0001109 ;
                                   sio:SIO_000300 ?severity_score_answer_value .

         ?severity_score_evaluation_process_date a obo:NCIT_C25164 ;
                                                  dct:Date ?severity_score_evaluation_process_date_value .
      }
}
